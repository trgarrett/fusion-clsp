(
  ; the ultimate util
  (include curry.clib)

  ; sha256
  (include sha256tree.clib)

  (defconstant b32 32)

  (defun-inline size_b32 (var)
    (= (strlen var) b32)
  )

  (defun calculate_coin_id (parent puzzlehash amount)
    (if (all (size_b32 parent) (size_b32 puzzlehash) (> amount -1))
      (sha256 parent puzzlehash amount)
      (x)
    )
  )

  ;; return the full puzzlehash for a singleton with the innerpuzzle curried in
  (defun-inline calculate_full_puzzle_hash (singleton_mod_hash launcher_id launcher_puzzle_hash inner_puzzle_hash)
    (curry_hashes_inline singleton_mod_hash
      ; calculate the singleton struct
      (sha256 2
        (sha256 1 singleton_mod_hash)
        (sha256 2
          (sha256 1 launcher_id)
          (sha256 1 launcher_puzzle_hash)
        )
      )
      inner_puzzle_hash
    )
  )

  ;; return the full puzzlehash for an NFT locked by the nft_inner_puzzle_hash
  (defun-inline calculate_nft_full_puzzle_hash (SINGLETON_MOD_HASH 
                                                LAUNCHER_PUZZLE_HASH 
                                                NFT_STATE_LAYER_MOD_HASH
                                                METADATA_UPDATER_PUZZLE_HASH_HASH
                                                nft_launcher_id
                                                metadata_hash
                                                nft_inner_puzzle_hash)
    (curry_hashes_inline 
      SINGLETON_MOD_HASH   
      (sha256 2
        (sha256 1 SINGLETON_MOD_HASH)
        (sha256 2
          (sha256 1 nft_launcher_id)
          (sha256 1 LAUNCHER_PUZZLE_HASH)
        )
      )
      (curry_hashes_inline 
        NFT_STATE_LAYER_MOD_HASH 
        (sha256 1 NFT_STATE_LAYER_MOD_HASH)
        metadata_hash
        METADATA_UPDATER_PUZZLE_HASH_HASH
        nft_inner_puzzle_hash
      )
    )  
  )

  ;;;(defun-inline calculate_nft_ownership_layer_puzzle_hash (NFT_OWNERSHIP_LAYER_MOD_HASH new_owner transfer_program_hash nft_inner_puzzle_hash)
  ;;;    (curry_hashes NFT_OWNERSHIP_LAYER_MOD_HASH
  ;;;      (sha256 1 nft_ownership_layer_mod_hash)
  ;;;      (sha256 1 new_owner)
  ;;;      transfer_program_hash
  ;;;      inner_puzzle_hash
  ;;;    )
  ;;;)

  ;; 
  ;;; given two lists of the same size, return a list of pairs ((l1[0] . l2[0]) ... (l1[n] . l2[n]))
  (defun list_of_pairs (l1 l2)
      (if (all (l l1) (l l2))
          ; a list remains
          (c (list (f l1) (f l2)) (list_of_pairs (r l1) (r l2)))
          ; either the end or unbalanced (error case)
          (if (any (l l1) (l l2))
            (x) ; unbalanced
            ()
          )
      )
  )

)
